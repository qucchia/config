#+title:Emacs Desktop Environment
#+PROPERTY: header-args:emacs-lisp :tangle ../../home/.emacs.d/lisp/desktop.el :mkdirp y

EXWM desktop configuration.

* Bash Script

#+begin_src shell :tangle ../../home/.bin/dotfiles-start-exwm :tangle-mode (identity #o755)

  #!/bin/sh
  exec dbus-launch --exit-with-session emacs -mm --debug-init

#+end_src

* Init Script

#+begin_src emacs-lisp

  (defun desktop-init ()
    "Initiate EXWM."
    ;; Make workspace 1 be the one where we land at startup
    (exwm-workspace-switch-create 1)

    ;; Set keyboard layout
    (start-process-shell-command "xmodmap" nil "xmodmap ~/.xmodmap")

    ;; Start Tor service
    (start-process-shell-command "tor" nil "sudo systemctl start tor@default.service")

    ;; Show time
    (setq display-time-default-load-average nil)
    (setq display-time-24hr-format t)
    (display-time-mode t)

    ;; Start server to allow opening files from other applications
    (server-start)

    ;; Blast my ears off with Star Wars fanfare!
    (racket-play-star-wars-music)
    (emms-mode-line-mode -1) ;; Turn modeline off

    (eshell))

#+end_src

* Window Manager

#+begin_src emacs-lisp :noweb yes

  (defun desktop-run-in-background (command)
    (let ((command-parts (split-string command "[ ]+")))
      (apply #'call-process `(,(car command-parts) nil 0 nil 0 ,@(cdr command-parts)))))

  (defun desktop-rename-buffer (name)
    (exwm-workspace-rename-buffer (if (> (length name) 57)
                                    (format "%s..." (substring name 0 57))
                                    name)))

  (defun desktop-update-title ()
    (if (string-match-p "—" exwm-title)
    (progn
      (setq l (reverse (split-string exwm-title " +— +")))
      (desktop-rename-buffer (string-join (cdr l))))
    (desktop-rename-buffer exwm-title)))

  (use-package exwm
    :custom
    (exwm-input-simulation-keys '(([?\M-h] . [left])
                                  ([?\M-j] . [down])
                                  ([?\M-k] . [up])
                                  ([?\M-l] . [right])
                                  ([?\M-0] . [home])
                                  ([?\M-$] . [end])
                                  ([?\M-b] . [prior])
                                  ([?\M-f] . [next]))
    :config
    (setq exwm-workspace-number 5)

    (add-hook 'exwm-update-title-hook #'desktop-update-title)
    (add-hook 'exwm-init-hook #'desktop-init)

    (require 'exwm-systemtray)
    (exwm-systemtray-enable)

    <<keybindings>>

    (exwm-enable))

#+end_src

** Keybindings

#+begin_src emacs-lisp :noweb-ref keybindings :tangle no

  (setq exwm-input-prefix-keys
        '(?\C-x ?\C-c ?\C-w ?\C-h
          ?\M-x ?\M-` ?\M-& ?\M-: ?\M-v
          ?\C-\M-j ?\C-\M-v ?\C-\M-n
          ?\s-x ?\s-c))



  ;; C-q will always send the next key directly
  (define-key exwm-mode-map [?\C-q] 'exwm-input-send-next-key)

  (setq exwm-input-global-keys
        `(
          ;; Reset to line-mode
          ([?\s-r] . exwm-reset)

          ;; Move between windows
          ([?\s-h] . windmove-left)
          ([?\s-j] . windmove-down)
          ([?\s-k] . windmove-up)
          ([?\s-l] . windmove-right)

          ;; Launch applications with shell command
          ([?\s-&] . (lambda (command)
                       (interactive (list (read-shell-command "$ ")))
                       (start-process-shell-command command nil command)))

          ;; Switch workspace
          ([?\s-w] . exwm-workspace-switch)
          ,@(mapcar (lambda (i)
                      `(,(kbd (format "s-%d" i)) .
                        (lambda ()
                          (interactive)
                          (exwm-workspace-switch-create ,i))))
                    (number-sequence 0 9))))

#+end_src

