#+title:Lieutenant
#+PROPERTY: header-args:emacs-lisp :tangle ../../home/.emacs.d/lisp/lieutenant.el

Lieutenant: keyboard layout, key maps and keybindings.

* See also

I also have some keybindings configuration in the following places:
- [[file:Desktop.org::*Keybindings][EXWM]]
- [[file:Psst.org::*Ivy][Ivy]]
- [[file:Psst.org::*Counsel][Counsel]]
- Some =C-h= prefixed shortcuts to [[file:Psst.org::*Helpful][Helpful]], and Helpful also has some of its own bindings
- [[file:George.org::*Evil text objects][Org text objects]]

* Keyboard layout

Keyboard layout: English US International with Alt Gr.

#+begin_src conf :tangle ../../root/etc/default/keyboard

  XKBMODEL=pc105
  XKBLAYOUT=us
  XKBVARIANT=altgr-intl
  XKBOPTIONS=
  BACKSPACE=guess

#+end_src

* Key map

I have a small key map that maps Caps Lock to Left Super and the key between Z and Left Shift to Escape, to reduce the distance needed to press these keys.

#+begin_src conf :tangle ../../home/.xmodmap

  clear control

  keycode 94 = Escape
  keycode 135 = Control_R

  add Control = Control_L Control_R

#+end_src

Set keymap function (bound to =s-x x=).

#+begin_src emacs-lisp

  (defun lieutenant-set-keymap ()
    "Set my custom keymap."
    (interactive)
    (start-process-shell-command "xmodmap" nil
      "xmodmap ~/.xmodmap")
    (message "Keymap set"))

#+end_src

* Emacs

Use escape key to quit.

#+begin_src emacs-lisp

  (global-set-key (kbd "<escape>") 'keyboard-escape-quit)

#+end_src

Register shortcuts: use =C-x r j <char>= to jump to them.

#+begin_src emacs-lisp

  ;; Files
  (set-register ?t '(file . "~/Documents/life/Tasks.org"))

  ;; Directories
  (set-register ?~ '(file . "~"))
  (set-register ?/ '(file . "/"))
  (set-register ?. '(file . "~/.dotfiles"))
  (set-register ?c '(file . "~/.dotfiles/config/emacs"))
  (set-register ?d '(file . "~/Downloads"))
  (set-register ?D '(file . "~/Documents"))
  (set-register ?m '(file . "~/Music"))
  (set-register ?p '(file . "~/Projects"))

#+end_src

* General

https://github.com/noctuid/general.el

#+begin_src emacs-lisp :noweb yes

  (use-package general
    :after evil
    :config

    ;; Leader keys: see below
    <<leader-keys>>

    (general-define-key
      "C-M-n" 'counsel-switch-buffer
      "M-v" 'scroll-other-window-down
      "<pause>" 'emms-pause))

#+end_src

** Leader keys

*** Main

These are the leader keys bound to =s-c=, and are the home to most of my custom keyboard shortcuts. I have organised them according to their leader keys, along with the [[Miscellaneous][Miscellaneous]] ones at the end.

#+begin_src emacs-lisp :noweb-ref leader-keys :noweb yes :tangle no

  (general-create-definer lieutenant-main-leader-keys
    :keymaps '(normal insert visual emacs)
    :prefix "s-c"
    :global-prefix "s-c")
  (lieutenant-main-leader-keys
    <<main-leader-keys>>)

#+end_src

**** =.=: Dotty

#+begin_src emacs-lisp :noweb-ref main-leader-keys :tangle no

  "."  '(:ignore t :which-key "dotty")
  ".o" '(dotty-open-tangled-file :which-key "open tangled file")

#+end_src

**** =b=: Bookmarks

#+begin_src emacs-lisp :noweb-ref main-leader-keys :tangle no

  "b"   '(:ignore t :which-key "bookmark")
  "bd"  '((lambda ()
            (interactive)
            (browse-url "https://discord.com/app"))
          :which-key "discord")

  "bf"  '(:ignore t :which-key "firefox")
  "bfp" '((lambda ()
            (interactive)
            (browse-url "about:preferences"))
          :which-key "preferences")

  "bg"  '((lambda ()
            (interactive)
            (browse-url "https://codeberg.org"))
          :which-key "codeberg")
  "bm"  '((lambda ()
            (interactive)
            (browse-url "https://moodle.ins-mediterrania.cat/login/index.php"))
          :which-key "moodle")
  "by"  '((lambda ()
            (interactive)
            (browse-url "https://www.youtube.com"))
          :which-key "youtube")

#+end_src

***** =bc=: Classroom

#+begin_src emacs-lisp :noweb-ref main-leader-keys :tangle no

  "bc"  '(:ignore t :which-key "classroom")
  "bca" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/Mzg3ODg5Nzg1Mzk0"))
          :which-key "english")
  "bcc" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/Mzg5NzM5MTU1NzE1"))
          :which-key "catalan")
  "bcd" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/MTY0ODg2NDY5MjAx"))
          :which-key "dibuix")
  "bce" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/Mzg5NzcxMzA1ODQ1"))
          :which-key "spanish")
  "bcf" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/Mzg5OTkwODAzNjYz"))
          :which-key "p.e.")
  "bcl" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/MzIwODUyMDAyNTQw"))
          :which-key "philosophy")
  "bcm" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/MzIwNjgyODcyMDM4"))
          :which-key "cmc")
  "bcq" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/MzkwMjkzNzQ0Mjc3"))
          :which-key "maths")
  "bct" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/MzkwMjMwODAxMTM4"))
          :which-key "technology")
  "bcu" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/MzU2OTczMzczMDU3"))
          :which-key "tutoria")
  "bcy" '((lambda ()
            (interactive)
            (browse-url "https://classroom.google.com/u/1/c/MzIwNjE5OTE2ODMz"))
          :which-key "physics")
#+end_src

**** =m=: Mode

#+begin_src emacs-lisp :noweb-ref main-leader-keys :tangle no

  "m"   '(:ignore t :which-key "mode")
  "m'"  '(electric-quote-mode :which-key "electric quote")
  "mf"  '(follow-mode :which-key "follow")
  "mi"  '(ivy-mode :which-key "ivy")
  "mo"  '(org-mode :which-key "org")
  "mv"  '(visual-fill-column-mode :which-key "visual fill")
  "mw"  '(whitespace-mode :which-key "whitespace")

#+end_src

**** =s=: Search

#+begin_src emacs-lisp :noweb-ref main-leader-keys :tangle no

  "s"  '(:ignore t :which-key search)
  "sc" '((lambda (term)
           (interactive (list (lieutenant-uri-encode (read-string "DIEC "))))
           (browse-url (format "https://dlc.iec.cat/Results?DecEntradaText=%s" term)))
         :which-key "diec")
  "sC" '((lambda (term)
           (interactive (list (lieutenant-uri-encode (read-string "DIEC reversed "))))
           (browse-url (format "https://dlc.iec.cat/Results?DefinicioText=%s&AllInfoMorf=False&OperEntrada=0&OperDef=3&OperEx=0&OperSubEntrada=0&OperAreaTematica=0&InfoMorfType=0&OperCatGram=False&AccentSen=False&CurrentPage=0&refineSearch=1&Actualitzacions=False" term)))
         :which-key "diec reverse")
  "sd" '((lambda (term)
           (interactive (list (lieutenant-uri-encode (read-string "DuckDuckGo "))))
           (browse-url (format "https://duckduckgo.com/?q=%s" term)))
         :which-key "duckduckgo")
  "sm" '((lambda (term)
           (interactive (list (lieutenant-uri-encode (read-string "MDN "))))
           (browse-url (format "https://developer.mozilla.org/en-US/search?q=%s" term)))
         :which-key "mdn")
  "sr" '((lambda (term)
           (interactive (list (lieutenant-uri-encode (read-string "RAE "))))
           (browse-url (format "https://dle.rae.es/%s" term)))
         :which-key "rae")
  "ss" '((lambda (term)
           (interactive (list (lieutenant-uri-encode (read-string "StartPage "))))
           (browse-url (format "https://www.startpage.com/do/dsearch?query=%s" term)))
         :which-key "startpage")
  "sw" '((lambda (term)
           (interactive (list (lieutenant-uri-encode (read-string "SwissCows "))))
           (browse-url (format "https://swisscows.com/web?query=%s" term)))
         :which-key "swisscows")
  "sy" '((lambda (term)
           (interactive (list (lieutenant-uri-encode (read-string "YouTube "))))
           (browse-url (format "https://www.youtube.com/results?search_query=%s" term)))
         :which-key "youtube")

#+end_src

***** TODO Make URI encode function

#+begin_src emacs-lisp

(defun lieutenant-uri-encode (string)
  "Encode STRING to URI (currently not working)."
  string)

#+end_src

**** =t=: Toggle

#+begin_src emacs-lisp :noweb-ref main-leader-keys :tangle no

  "t"     '(:ignore t :which-key "toggle")
  "te"    '(emms-mode-line-toggle :which-key "emms modeline")
  "tp"    '(qucchia-toggle-tor :which-key "proxy")
  "tt"    '(counsel-load-theme :which-key "choose theme")
  "ts"    '(lieutenant-toggle-spelling :which-key "spelling")
  "t s-s" '(hydra-text-scale/body :which-key "scale text")

#+end_src

The function below toggles the spellchecking, and decides whether to use =flyspell-mode= or =flyspell-prog-mode=.

#+begin_src emacs-lisp

  (defun lieutenant-toggle-spelling ()
    (interactive)
    (if flyspell-mode
      (progn
        (flyspell-mode 0)
        (message "Spellcheck disabled"))
      (progn
        (if (or (derived-mode-p 'prog-mode)
                (derived-mode-p 'conf-mode))
          (flyspell-prog-mode)
          (flyspell-mode))
        (message "Spellcheck enabled"))))

#+end_src

**** =r=: Racket

#+begin_src emacs-lisp :noweb-ref main-leader-keys :tangle no

  "r"  '(:ignore t :which-key "racket")
  "ra" 'racket-add-track-to-favourites
  "rf" 'racket-play-favourites
  "ro" 'racket-open-favourites
  "rr" 'racket-remove-track-from-favourites

#+end_src

**** =s-p=: Lookup password

#+begin_src emacs-lisp :noweb-ref main-leader-keys :tangle no

  "s-p" '(lieutenant-lookup-password :which-key "password")

#+end_src

#+begin_src emacs-lisp

  (defun lieutenant-lookup-password (name)
    "Retrieve the password NAME from pass and copy it to the clipboard."
    (interactive (list (read-string "Password name: ")))
    (let ((process (start-process-shell-command "pass" nil
            (format "pass %s" name))))
      (set-process-filter process
        ;; This function is called after the process completes
        (lambda (process string)
          (let ((string (s-trim string)))
            (if (string-match-p "^Error: " string)
              (message string)
              (progn (kill-new string)
                (message "Password copied"))))))))

#+end_src

**** Miscellaneous

#+begin_src emacs-lisp :noweb-ref main-leader-keys :tangle no

  "s-l" '(counsel-linux-app :which-key "linux app")
  "k"   '(counsel-descbinds :which-key "keybindings")
  "p"   '(emms-pause :which-key "pause music")
  "u"   '(browse-url :which-key "url")
  "x"   '(lieutenant-set-keymap :which-key "set keymap")

#+end_src

*** Apps

#+begin_src emacs-lisp :noweb-ref leader-keys :noweb yes :tangle no

  (general-create-definer lieutenant-app-leader-keys
    :keymaps '(normal insert visual emacs)
    :prefix "s-o"
    :global-prefix "s-o")
  (lieutenant-app-leader-keys
    <<app-leader-keys>>)

#+end_src

**** Emacs apps

#+begin_src emacs-lisp :noweb-ref app-leader-keys :tangle no

  "e"    '(emms :which-key "emms")
  "s-e" '(eshell :which-key "eshell")
  "i"    '(ibuffer :which-key "ibuffer")
  "s"    '(shell :which-key "shell")
  "t"    '(term :which-key "term")
  "v"    '(vterm :which-key "vterm")

#+end_src

**** Linux apps

#+begin_src emacs-lisp

  (defun lieutenant-open-app (command name class &optional force-new)
    "Create a new process with COMMAND and NAME, or open the first
  buffer matching CLASS if it exists.  If FORCE-NEW is non-nil,
  create a new process regardless."
    (when (or force-new
      (not
        (let ((buffer
          (-any
            (lambda (buffer) (when
              (string= (buffer-local-value 'exwm-class-name buffer) class)
              buffer))
            (buffer-list))))
          (when buffer (switch-to-buffer buffer)
          buffer))))
      (start-process-shell-command name (format "*%s log*" name) command)))

  (defun lieutenant-app (command name class)
    "Prompt user to select either a buffer from app to open, create
  a new process, or open a URL.  COMMAND is the command to run if
  selecting to create a new process.  NAME is the name of the app.
  CLASS is the value of `exwm-class-name' to filter buffers through."
    (ivy-read (format "Switch to %s buffer, or insert URL or search term: " name)
      (append
        (seq-map (lambda (buffer) (format "Tab: %s" (buffer-name buffer)))
          (seq-filter (lambda (buffer) (when
            (string= (buffer-local-value 'exwm-class-name buffer) class)
              buffer))
            (buffer-list)))
        '("New tab"))
      :action (lambda (choice)
                (cond ((string-match-p "^Tab: " choice)
                       (switch-to-buffer (replace-regexp-in-string "^Tab: " "" choice)))
                      ((string= choice "New tab")
                       (start-process-shell-command name (format "*%s log*" name) command))
                      ((string-match-p browse-url-button-regexp choice)
                       (start-process-shell-command name (format "*%s log*" name)
                         (format "%s %s" command choice)))
                      (t
                       (start-process-shell-command name (format "*%s log*" name)
                         (format "%s https://duckduckgo.com/?q=%s" command choice)))))))

  (defmacro lieutenant-app-command (command name class &optional which-key)
    "Macro to create command entry for keybindings to
  `lieutenant-app'.  The arguments are the same as that, as well as
  WHICH-KEY, the description for which-key."
    (list 'list
      (list 'lambda (list)
        (list 'interactive)
        (list  'lieutenant-app command name class))
      :which-key which-key))

#+end_src

#+begin_src emacs-lisp :noweb-ref app-leader-keys :tangle no

  "f" (lieutenant-app-command "firefox" "Firefox" "firefox" "firefox")

#+end_src

**** Web apps

#+begin_src emacs-lisp

  (defun lieutenant-open-web-app (url name-regexp &optional force-new)
    "Open a new web-page in URL, or open the buffer matching
    NAME-REGEXP if it exists.  If FORCE-NEW is non-nil, open a new
    window regardless."
    (when (or force-new
      (not
        (let ((buffer
                (-any
                  (lambda (buffer) (when
                    (string-match-p name-regexp
                      (replace-regexp-in-string " â€” Firefox$" ""
                        (buffer-name buffer)))
                    buffer))
                  (buffer-list))))
          (when buffer (switch-to-buffer buffer))
          buffer)))
      (browse-url url)))

  (defmacro lieutenant-web-app-command (url name-regexp which-key)
    (list 'list
      (list 'lambda (list '&optional 'arg)
        (list 'interactive "^p")
        (list 'lieutenant-open-web-app url name-regexp
          (list 'not (list 'eq 'arg 1))))
      :which-key which-key))

#+end_src

#+begin_src emacs-lisp :noweb-ref app-leader-keys :tangle no

  "d" (lieutenant-web-app-command
        "https://drive.google.com/drive/u/1"
        "^Google Drive$"
        "drive")
  "g" (lieutenant-web-app-command
        "https://mail.google.com/mail/u/1/#inbox="
        " Mail\\| - Gmail$"
        "gmail")
  "w" (lieutenant-web-app-command
        "https://web.whatsapp.com"
        "^\\(([0-9]+) \\)?WhatsApp$"
        "whatsapp")
  "y" (lieutenant-web-app-command
        "https://youtube.com"
        "^\\( - \\)?Youtube"
        "youtube")

#+end_src

*** Acronyms

These leader keys are bound to =s-x= and follow an acronym pattern, which is easy to remember.

#+begin_src emacs-lisp :noweb-ref leader-keys :noweb yes :tangle no

    (general-create-definer lieutenant-acronym-leader-keys
      :keymaps '(normal insert visual emacs)
      :prefix "s-x"
      :global-prefix "s-x")
    (lieutenant-acronym-leader-keys
      <<acronym-leader-keys>>)

#+end_src

#+begin_src emacs-lisp :noweb-ref acronym-leader-keys :tangle no

  "dtw" '(delete-trailing-whitespace :which-key "delete-trailing-whitespace")
  "l"   '(:ignore t :which-key "list")
  "lb"  '(list-bookmarks :which-key "bookmarks")
  "lcc" '(list-charset-chars :which-key "charset-chars")
  "lcd" '(list-colors-display :which-key "colors-display")
  "lch" '(list-command-history :which-key "command-history")
  "lcs" '(list-coding-systems :which-key "coding-systems")
  "lCs" '(list-character-sets :which-key "character-sets")
  "lfd" '(list-faces-display :which-key "paces-display")
  "lfe" '(list-flycheck-errors :which-key "flycheck-errors")
  "lp"  '(list-packages :which-key "packages")
  "lP"  '(list-processes :which-key "processes")

#+end_src
* Evil

=evil=: extensible vi layer
Links: [[https://github.com/emacs-evil/evil][GitHub]], [[info:evil][Info]]

#+begin_src emacs-lisp

  (use-package evil
    :init
    (setq evil-want-integration t)
    (setq evil-want-C-i-jump t)
    (setq evil-want-Y-yank-to-eol t)
    (setq evil-want-fine-undo t)
    :config
    (evil-mode 1)
    (setq-default evil-shift-width 2)
    (define-key evil-insert-state-map (kbd "C-g") 'evil-normal-state)

    (define-key evil-ex-completion-map (kbd "M-p") 'previous-complete-history-element)
    (define-key evil-ex-completion-map (kbd "M-n") 'next-complete-history-element)
    (define-key evil-eval-map (kbd "M-p") 'previous-complete-history-element)
    (define-key evil-eval-map (kbd "M-n") 'next-complete-history-element)

    ;; Use visual line motions even outside of visual-line-mode buffers
    (evil-global-set-key 'motion "j" 'evil-next-visual-line)
    (evil-global-set-key 'motion "k" 'evil-previous-visual-line)

    ;; Set some initial states
    (evil-set-initial-state 'messages-buffer-mode 'normal)
    (evil-set-initial-state 'dashboard-mode 'normal)
    (evil-set-initial-state 'git-commit-mode 'insert))

#+end_src

=evil-collection=: https://github.com/emacs-evil/evil-collection

#+begin_src emacs-lisp

  (use-package evil-collection
    :after evil
    :config
    (evil-collection-init))

#+end_src

** Record macro

#+begin_src emacs-lisp

  (with-eval-after-load 'evil
    (evil-define-command evil-record-macro (register)
      "Record a keyboard macro into REGISTER.
    If REGISTER is :, /, or ?, the corresponding command line window
    will be opened instead."
      :keep-visual t
      :suppress-operator t
      (interactive
      (list (unless (and evil-this-macro defining-kbd-macro)
              (or evil-this-register (evil-read-key "q-")))))
      (let (last-macro)
        (cond
        ((eq register ?\C-g)
          (keyboard-quit))
        ((and evil-this-macro defining-kbd-macro)
          (setq evil-macro-buffer nil)
          (condition-case nil
              (setq last-macro (evil-end-and-return-macro))
            (error nil))
          (when last-macro
            (evil-set-register evil-this-macro last-macro))
          (setq evil-this-macro nil))
        ((eq register ?:)
          (evil-command-window-ex))
        ((eq register ?/)
          (evil-command-window-search-forward))
        ((eq register ??)
          (evil-command-window-search-backward))
        ((or (<= ?0 register ?9)
              (<= ?a register ?z)
              (<= ?A register ?Z))
          (when defining-kbd-macro (end-kbd-macro))
          (setq evil-this-macro register)
          (evil-set-register evil-this-macro nil)
          (kmacro-start-macro nil)
          (setq evil-macro-buffer (current-buffer)))
        (t (error "Invalid register"))))))

#+end_src

